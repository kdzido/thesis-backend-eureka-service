buildscript {
    ext {
        springBootVersion = '1.5.10.RELEASE'
    }
    repositories {
        mavenCentral()
//        maven { url 'http://artifactory:8071/artifactory/libs-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE')
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.4'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-remote-api'

import org.apache.tools.ant.filters.*
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'pl.pja.13868.news'
version '0.2.0'

repositories {
    mavenLocal()
    // TODO own repo (cloud | private)
    mavenCentral()
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/java')
            srcDir file('src/integTest/groovy')
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR2"
    }
}

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-eureka-server')

    testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile("cglib:cglib-nodep:${cglibVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test')

    integTestCompile sourceSets.main.output
    integTestCompile("junit:junit:${junitVersion}")
    integTestCompile("cglib:cglib-nodep:${cglibVersion}")
    integTestCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    integTestCompile "org.spockframework:spock-core:${spockVersion}"

}

task integTest(type: Test) {
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    description ='Integration tests'
    group = 'verification'
}

integTest.mustRunAfter test

//task createBuildInfoFile << {
//    def buildInfo = new File("$buildDir/build-info.properties")
//    def properties = new Properties()
//    properties.setProperty("version", project.version.toString())
////    properties.setProperty("timestamp", project.buildTimestamp) // TODO timestamp
//    properties.store(buildInfo.newWriter(), null)
//}

jar {
//    dependsOn createBuildInfoFile
    baseName = 'eureka-service'

    from(buildDir) {
        include 'build-info.properties'
        into('META-INF/')
    }
}

task copyJars(dependsOn: jar, type: Copy) {
    from jar
    into "$buildDir/dockerfile"
}

task copyDockerFiles(dependsOn: [copyJars], type: Copy) {
    from 'src/docker'
    into "$buildDir/dockerfile"

    String springBootJar = "eureka-service-${project.version}.jar"
    filter(ReplaceTokens, tokens: [springBootJar: springBootJar])
}

docker {
    registryCredentials {
//        url = 'https://index.docker.io/v1/'
        username = System.env.DOCKERHUB_CREDS_USR
        password = System.env.DOCKERHUB_CREDS_PSW
        email = "xtofek@gmail.com"
    }
}

// TODO use DockerApplication with Boot
task buildDockerImage(type: DockerBuildImage) {
    dependsOn copyDockerFiles
    inputDir = project.file("$buildDir/dockerfile/Dockerfile").parentFile
    // TODO include build-info.properties

    String dockerTag = System.env.PIPELINE_BUILD_ID ?: "snapshot"
    tag = "qu4rk/thesis-eurekaservice:${dockerTag}"
}

// TODO publish to artifactory?
